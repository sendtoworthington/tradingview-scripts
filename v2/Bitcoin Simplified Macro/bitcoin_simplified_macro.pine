//@version=4
study(title="My:Bitcoin Simplified Macro", shorttitle="mbsm", overlay=true)

sma_21_D  = security(syminfo.tickerid, '1D', sma(close, 21))
sma_7_W = security(syminfo.tickerid, '1D', sma(close, 50))
sma7WeeksPlot = plot(sma_7_W, color=color.new(color.blue, 50), linewidth=1, title="SMA 50 D")

ema_21_W  = security(syminfo.tickerid, '1D', ema(close, 147))
ema21WeeksPlot = plot(ema_21_W, color=color.new(color.white, 100), linewidth=1, title="EMA 21 W")

sma_200_D = security(syminfo.tickerid, '1D', sma(close, 200))
sma200DaysPlot = plot(sma_200_D, color=color.new(color.white, 100), linewidth=1, title="SMA 200 D")

sma_4_Y = security(syminfo.tickerid, '1D', sma(close, 1458))
sma4YearsPlot = plot(sma_4_Y, color=color.new(color.white, 100), linewidth=1, title="SMA 4 Y")

sma_200_W = security(syminfo.tickerid, '1D', sma(close, 1400))
sma200WeeksPlot = plot(sma_200_W, color=color.new(color.white, 100), linewidth=1, title="SMA 200 W")

// Bull Market Support Band - 21 weeks ema + 200 days sma
bullMarketSupportBandColor = color.lime
fill(ema21WeeksPlot, sma200DaysPlot, color=color.new(bullMarketSupportBandColor, 75))

// Market Support Band - 4 Years sma + 200 weeks sma
marketSupportBandColor = color.blue
fill(sma4YearsPlot, sma200WeeksPlot, color=color.new(marketSupportBandColor, 75))

///////////////////////////////////////////////////////////
// PI Cycle Pre Out - Out - In
///////////////////////////////////////////////////////////
dailyMayersMultiple = close / sma_200_D
mayersMultiple06 = sma_200_D * 0.6
mayersMultiple24 = sma_200_D * 2.4
mayersMultiple5 = sma_200_D * 5
sma_350_D_By_2 = security(syminfo.tickerid, '1D', sma(close, 350)*2)
sma_111_D = security(syminfo.tickerid, '1D', sma(close, 111))
sma111DaysPlot = plot(sma_111_D, color=color.new(color.lime, 50), linewidth=1, title="SMA 111 D")
dailyClose = security(syminfo.tickerid, '1D', close)

cycleInBackgroundColor = #39d348 // Lime Green
cycleOutBackgroundColor = #ed6565 // Candy Oink
cycleQuestionBackgroundColor = #648aea // Blue Pastel
cycleIPreOutBackgroundColor = #fcac00 // Honey Yellow

cycleIn = crossover(mayersMultiple06, sma_21_D) ? dailyClose + (dailyClose/100 * 10) : na
plotshape(cycleIn, style=shape.labelup,size=size.small, text="Cycle In", color=cycleInBackgroundColor, textcolor=color.black, location=location.belowbar)

cycleOut = crossunder(sma_350_D_By_2, sma_111_D) ? dailyClose + (dailyClose/100 * 10) : na
plotshape(cycleOut, style=shape.labeldown,size=size.small, text="Cycle Out", color=cycleOutBackgroundColor, textcolor=color.black, location=location.abovebar)


var cycleOutCrossed = 0
var cycleInCrossed = 0
var currentPreInValue = 0
var currentOutInValue = 0


// Cycle Out to Cycle In percentage
if (cycleOutCrossed == 0 and crossunder(sma_350_D_By_2, sma_111_D))
    cycleOutCrossed := 1
    cycleInCrossed := 0
if (cycleInCrossed == 0 and crossover(mayersMultiple06, sma_21_D))
    cycleOutCrossed := 0
    cycleInCrossed := 1
currentOutInValue:= round(100 + (sma_111_D - sma_350_D_By_2) / sma_350_D_By_2 * 100)
currentPreInValue := round(100 + (mayersMultiple06 - sma_21_D) / sma_21_D * 100)

///////////////////////////////////////////////////////////
// DCA IN - Feigenbaum constant
///////////////////////////////////////////////////////////
timeDuration = time - time[1]
timeDurationSinceGenesis = round((time - 1231502400 * 1000) / timeDuration)
lm = round(timeDurationSinceGenesis/4.669201609102990671853203820466)
clm = close[lm]
plotshape(crossover(clm[3], close[3]) and clm > close, text='DCA', location=location.belowbar, style=shape.triangleup, color=color.yellow, size=size.tiny)
bgcolor(clm > close ? color.new(color.yellow, 95) : color.new(color.yellow, 100))

///////////////////////////////////////////////////////////
// Puell Multiple
///////////////////////////////////////////////////////////

mining_revenue_1_D = security("QUANDL:BCHAIN/MIREV", "1D", close[1], barmerge.gaps_off, barmerge.lookahead_on)
sma_355_D = security("QUANDL:BCHAIN/MIREV", "1D", sma(close, 365)[1], barmerge.gaps_off, barmerge.lookahead_on)
puellMultiple = mining_revenue_1_D / sma_355_D
bgcolor(puellMultiple < 0.5 ? color.new(#039b42, 20) : puellMultiple > 6 ? color.new(#d3303e, 20) : na)

///////////////////////////////////////////////////////////
// Fear & Greed
///////////////////////////////////////////////////////////

pmacd      = security(syminfo.tickerid, "D", (close/ema(close, 89) - 1) * 100, barmerge.gaps_off, barmerge.lookahead_on)
ror        = security(syminfo.tickerid, "D", (close - close[89])/close[89] * 100, barmerge.gaps_off, barmerge.lookahead_on)
accDist    = security(syminfo.tickerid, "D", close==high and close==low or high==low ? 0 : ((2 * close - low - high) / (high - low)), barmerge.gaps_off, barmerge.lookahead_on)
vol        = security(syminfo.tickerid, "D", volume, barmerge.gaps_off, barmerge.lookahead_on)
moneyFlow  = if nz(vol) != 0
    sum(accDist * vol, 13) / sum(vol, 13) * 100
volatility = security("BVOL24H", "D", -(close/ema(close, 89) - 1) * 100)
btcdom     = security("BTC.D",   "D",  (1 - close[13] / close) * 100)

val1 = volatility

val2 = btcdom

fearAndGreedValue = if nz(vol) == 0
    rma((pmacd + ror + val1 + val2) / 4, 1)
else
    rma((pmacd + ror + moneyFlow + val1 + val2) / 5, 1)

f_getText(value) =>
    if value > 50  
        "Extreame Greed" 
    else if value > 20
        "Greed"
    else if value < -25
        "Fear" 
    else if value < -40
        "Extreame Fear"
    else
        "Neutral"

f_getColor(value) =>
    if (value > 50)
        #E3242B // Mid Red
    else if (value > 20)
        #E6910E // Orange
    else if (value > 0)
        color.white
    else if (value > -25)
        #C7EA46 // Light Green
    else
        #39FF14 // Dark Green

///////////////////////////////////////////////////////////
// Text Info
///////////////////////////////////////////////////////////

var table panel = table.new("top_center", 10, 20, border_width = 1)
table.cell(panel, 1, 0, "Cycle Out to Cycle In (0% > 100%)", bgcolor = color.white, text_size=size.small)
inOutColor = currentPreInValue < 25 ? color.red : currentPreInValue > 75 ? color.lime : color.white
table.cell(panel, 2, 0, tostring(currentPreInValue) + "%", bgcolor = inOutColor, text_size=size.small)
table.cell(panel, 1, 1, "Cycle In to Cycle Out (0% > 100%)", bgcolor = color.white, text_size=size.small)
outInColor = currentOutInValue < 25 ? color.red : currentOutInValue > 75 ? color.lime : color.white
table.cell(panel, 2, 1, tostring(currentOutInValue) + "%", bgcolor = outInColor, text_size=size.small)
table.cell(panel, 1, 2, "Puell Multiple (Buy < 0.5 / Sell > 6)", bgcolor = color.white, text_size=size.small)
puellColor = puellMultiple < 0.5 ? color.lime : puellMultiple > 6 ? color.red : color.white
table.cell(panel, 2, 2, tostring(puellMultiple, '#.#'), bgcolor = puellColor, text_size=size.small)
table.cell(panel, 1, 3, "Fear & Greed", bgcolor = color.white, text_size=size.small)
table.cell(panel, 2, 3, tostring(round(fearAndGreedValue, 2)) + "% (" + f_getText(fearAndGreedValue) + ")", bgcolor = f_getColor(fearAndGreedValue), text_size=size.small)
table.cell(panel, 1, 4, "Mayers Mulitple (Buy < 0.7 / Sell > 2.4)", bgcolor = color.white, text_size=size.small)
mayersMultipleColor = dailyMayersMultiple < 0.7 ? color.lime : puellMultiple > 2.4 ? color.red : color.white
table.cell(panel, 2, 4, tostring(round(dailyMayersMultiple, 2)), bgcolor = mayersMultipleColor, text_size=size.small)